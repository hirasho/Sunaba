include "ioMap.txt"
include "utility.txt"

def System_sync()
   memory[MEM_SYNC] -> 1

def System_disableAutoSync()
   memory[MEM_DISABLE_AUTO_SYNC] -> 1

def System_enableAutoSync()
   memory[MEM_DISABLE_AUTO_SYNC] -> 0

def System_break()
   memory[MEM_BREAK] -> 1

def System_outputChar(n)
   memory[MEM_OUTPUT_CHAR] -> n
 
def System_outputString(start, count)
   i -> 0
   while i < count
      memory[MEM_OUTPUT_CHAR] -> start[i]
      i -> i + 1

def System_outputNumber(n)
   if n < 0
      System_outputChar(45)
      n -> -n
   v -> 10 * 10000 * 10000 #10億
   written -> 0
   while v > 0
      k -> n / v
      if k = 0
         if written + (v = 1)
            System_outputChar(48 + k)
      if k != 0
         System_outputChar(48 + k)
	 written -> 1
      n -> n - (v * k)
      v -> v / 10

def System_outputNewLine()
   System_outputChar(10)

def System_die(errorNumber)
   #error:番号
   set6(0, 101, 114, 114, 111, 114, 58) #どうせ死ぬので0番から使う
   System_outputString(0, 6)
   System_outputNumber(errorNumber)
   System_outputNewLine()
   memory[-1] -> 0 #自殺

def System_setResolution(width, height)
   memory[MEM_SET_SCREEN_WIDTH] -> width
   memory[MEM_SET_SCREEN_HEIGHT] -> height
   memory[MEM_SYNC] -> 1

def System_screenWidth()
   out -> memory[MEM_SCREEN_WIDTH]

def System_screenHeight()
   out -> memory[MEM_SCREEN_HEIGHT]

def System_playSound(channel, frequency, dumping, amplitude)
   if (channel < 0) + (channel > 3)
      System_die(Error_OUT_OF_RANGE)
   memory[MEM_SET_SOUND_FREQUENCY0 + channel] -> frequency
   memory[MEM_SET_SOUND_DUMPING0 + channel] -> dumping
   memory[MEM_SET_SOUND_AMPLITUDE0 + channel] -> amplitude
